import("//build/app_config.gni")
import("//build_overrides/chip.gni")
import("${chip_root}/src/app/chip_data_model.gni")
import("${chip_root}/src/app/common_flags.gni")
import("${chip_root}/src/lib/core/core.gni")
import("${chip_root}/src/lib/lib.gni")

chip_data_model("mobgtw-zap") {
  zap_file = "//mobgtw.zap"
  is_server = true

  cflags = [ "-DDYNAMIC_ENDPOINT_COUNT=100" ]
}

config("includes") {
  include_dirs = [ "//src" ]
}

config("app_config") {
  defines = [
    "DATABASE_FILE=\"${database_file}\"",
    "DATABASE_PRAGMA_SQL=\"${database_pragma_sql}\"",
    "MOBILUS_DSN=\"${mobilus_dsn}\"",
  ]
}

executable("mobmatter") {
  sources = [
    "include/AppConfig.h",
    "include/CHIPProjectAppConfig.h",
    "include/SqliteDatabaseSchema.h",
    "src/main.cpp",
  ]

  deps = [
    "//src/application",
    "//src/common/logging:stdio",
    "//src/common/logging:syslog",
    "//src/driven_adapters/matter/reporting:cover",
    "//src/driven_adapters/matter/zcl:cover_endpoint",
    "//src/driven_adapters/mobilus:cover_control",
    "//src/driven_adapters/mobilus:device_management",
    "//src/driven_adapters/persistence/sqlite",
    "//src/driving_adapters/matter/cluster_stubs",
    "//src/driving_adapters/matter/cover_cluster",
    "//src/driving_adapters/mobilus:composite_device_event_handler",
    "//src/driving_adapters/mobilus:device_event_subscriber",
    "//src/driving_adapters/mobilus:device_initializer",
    "//src/driving_adapters/mobilus:device_name_syncer",
    "//src/driving_adapters/mobilus:device_state_syncer",
    "//src/driving_adapters/mobilus/cover",
    "//src/driving_adapters/mobilus/generic",
    "//src/matter",
    "//src/matter/event_loop",
    "//src/matter/logging",
    "//src/matter/persistence:sqlite",
    "//third_party/mobilus_gtw_client",
  ]

  include_dirs = [
    "include",
    "src",
  ]

  configs += [ ":app_config" ]

  cflags = [ "-Wconversion" ]

  output_dir = root_out_dir
}

group("default") {
  deps = [ ":mobmatter" ]
}

group("tests") {
  testonly = true
  deps = [ "//tests" ]
}
